plugins {
    id "com.jfrog.bintray" version "1.2"
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

group = 'com.craigburke'
version = '1.0.0'

apply plugin: 'groovy'
apply plugin: 'maven-publish'

configurations {
    provided
    testCompile { extendsFrom provided }
}

dependencies {
    compile 'com.bertramlabs.plugins:asset-pipeline-core:2.0.12'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

bintray {
    user = project.hasProperty('bintrayUsername') ? project.bintrayUsername : ''
    key = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : ''
    publications = ['maven']

    pkg {
        repo = 'asset-pipeline'
        userOrg = 'craigburke'
        name = 'js-closure-wrap-asset-pipeline'
        licenses = ['Apache-2.0']
    }

}


publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'js-closure-wrap-asset-pipeline'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'js-closure-wrap-asset-pipeline'
                    description 'JS closure wrapper extension for the Asset Pipeline Library.'
                    url 'https://github.com/craigburke/js-closure-wrap-asset-pipeline'
                    scm {
                        url 'https://github.com/craigburke/js-closure-wrap-asset-pipeline'
                        connection 'scm:https://github.com/craigburke/js-closure-wrap-asset-pipeline.git'
                        developerConnection 'scm:https://github.com/craigburke/js-closure-wrap-asset-pipeline.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'craigburke'
                            name 'Craig Burke'
                            email 'craig@craigburke.com'
                        }
                    }
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintrayUpload.dependsOn build, sourcesJar, javadocJar
